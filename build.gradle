plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'edu.sc.seis.launch4j' version '3.0.5'
    id 'maven-publish'
    id 'application'
    id 'java'
}

group = 'net.foulest'
version = '0.4.0'
description = 'RepairKit'

compileJava.options.encoding = 'UTF-8'

repositories {
    // Maven central repository
    mavenCentral()
    mavenLocal()

    // Local libraries
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    // JNA platform - for registry access
    // https://mvnrepository.com/artifact/net.java.dev.jna/jna-platform
    implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.14.0'

    // JetBrains Annotations - for code inspection and documentation
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '24.1.0'

    // Lombok - for reducing boilerplate code
    // https://projectlombok.org
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

launch4j {
    // Executable file information
    outfile = "${project.name}-${project.version}.exe"
    mainClassName = "${project.group}.repairkit.RepairKit"
    icon = "${projectDir}/RepairKit.ico"

    // Java runtime information
    jreMinVersion = '1.8.0'
    initialHeapSize = 128
    maxHeapSize = 1024

    // Application version information
    version = "${project.version}"
    textVersion = "${project.version}"
    fileDescription = 'RepairKit by Foulest'
    copyright = "2024"
    productName = "${project.name}"
    internalName = "${project.name}"

    // Custom manifest file for Windows
    manifest = "${projectDir}/RepairKit.manifest"
}

tasks {
    shadowJar {
        archiveFileName.set("${project.name}-${project.version}.jar")
        minimize()

        mainClassName = 'net.foulest.repairkit.RepairKit'
        relocate 'net.java', 'net.foulest.repairkit.shaded.net.java'
        relocate 'org.jetbrains', 'net.foulest.repairkit.shaded.org.jetbrains'
    }

    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        // Set the 'Main-Class' attribute in the JAR manifest
        manifest {
            attributes 'Main-Class': 'net.foulest.repairkit.RepairKit'
        }
    }

    compileJava {
        dependsOn(clean)
    }

    build {
        dependsOn(shadowJar)
    }

    createExe {
        dependsOn(shadowJar)
    }

    processResources {
        filesMatching('**/plugin.yml') {
            expand(project.properties)
        }
    }

    tasks.register('sourceJar', Jar) {
        from sourceSets.main.allJava
    }

    jar.configure {
        archiveClassifier = 'default'
    }

    shadowJar.configure {
        archiveClassifier = null
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "net.foulest.repairkit"
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}
